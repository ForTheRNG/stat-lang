Name ::= [A-Za-z_][A-Za-z_0-9]*
Number ::= -?0|[1-9][0-9]*
FunctionName ::= FunctionName . Name
FunctionName ::= Name

Namespace ::= ' < Name > ' Namespace
Namespace ::= ' Name ' Namespace
Namespace ::= ~ Name { Namespace } Namespace
Namespace ::= Name = Expression Namespace
Namespace ::= Expression

Expression ::= @ FunctionName FunctionParams
Expression ::= ListOperation @ FunctionName FunctionParams
FunctionParams ::= FunctionParams , ListOperation
FunctionParams ::= ListOperation

ListOperation ::= [ ListOperation
ListOperation ::= ListOperation ]
ListOperation ::= ListOperation | ListOperation
ListOperation ::= TernaryOperation

TernaryOperation ::= TernaryOperationLogical ? TernaryOperation : TernaryOperation
TernaryOperation ::= TernaryOperationLogical ? Addition : Addition
TernaryOperation ::= TernaryOperationLogical
TernaryOperation ::= Addition

TernaryOperationLogical ::= TernaryOperationLogical ? TernaryOperationLogical : TernaryOperationLogical
TernaryOperationLogical ::= Logical

Logical ::= Logical && Logical
Logical ::= Logical || Logical
Logical ::= ! Logical
Logical ::= Comparison

Comparison ::= Addition > Addition
Comparison ::= Addition >= Addition
Comparison ::= Addition < Addition
Comparison ::= Addition <= Addition
Comparison ::= Addition == Addition
Comparison ::= Addition != Addition

Addition ::= Multiplication + Addition
Addition ::= Multiplication - Addition
Addition ::= Multiplication

Multiplication ::= Multiplication * DieRoll
Multiplication ::= Multiplication / DieRoll
Multiplication ::= Multiplication % DieRoll
Multiplication ::= DieRoll

DieRoll ::= DieRoll # Term
DieRoll ::= Term

Term ::= $ Name
Term ::= $ Number
Term ::= Number
Term ::= ( Expression )

Whitespace ::= Whitespace Comment Whitespace
Whitespace ::= [\n\t\r\v ]*
Comment ::= "[^"]*"